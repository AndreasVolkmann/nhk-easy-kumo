group 'me.avo'
version '1.2'

buildscript {
    ext {
        kotlin_version = '1.2.71'
        log4j_version = '2.10.0'
        deeplearning_version = '0.9.1'
        junit_version = '5.2.0'
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.20.0"
    }
}

apply plugin: "com.github.ben-manes.versions"
apply plugin: 'kotlin'

task fatJar(type: Jar) {
    manifest {
        attributes 'Compile-Title': project.name,
                'Compile-Version': version,
                'Main-Class': 'me.avo.kumo.ApplicationKt'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    with jar
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://dl.bintray.com/kotlin/exposed" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    /* Logging */
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4j_version
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4j_version
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j_version
    compile group: "org.slf4j", name: "slf4j-simple", version: "1.7.25"

    /* Browser automation */
    compile "org.seleniumhq.selenium:selenium-chrome-driver:3.12.0"
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.3'

    /* Audio Tools */
    compile 'com.iheartradio.m3u8:open-m3u8:0.2.6'
    compile 'org.jcodec:jcodec:0.2.3'
    compile 'com.googlecode.soundlibs:jlayer:1.0.1.4'
    compile 'com.googlecode.soundlibs:mp3spi:1.9.5.4'

    /* Database */
    compile 'org.jetbrains.exposed:exposed:0.10.3'
    compile 'org.xerial:sqlite-jdbc:3.23.1'
    compile 'org.mongodb:mongodb-driver:3.5.0'

    /* NLP */
    compile 'com.atilika.kuromoji:kuromoji-ipadic:0.9.0'
    compile "org.deeplearning4j:deeplearning4j-nlp:$deeplearning_version"
    compile "org.nd4j:nd4j-native-platform:$deeplearning_version"

    /* Utility */
    compile group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.6"
    compile 'com.github.salomonbrys.kotson:kotson:2.5.0'
    compile 'com.apurebase:arkenv:1.2.0'
    compile 'net.jodah:failsafe:1.1.1'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:0.23.3'
    compile 'org.kodein.di:kodein-di-generic-jvm:5.1.0'

    testCompile "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testCompile "org.amshove.kluent:kluent:1.46"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}

test {
    useJUnitPlatform {
        excludeTags "local"
        includeEngines 'junit-jupiter'
    }
    testLogging {
        events "failed"
        exceptionFormat "full"
    }

    failFast = false
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}